// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext {
        compose_compiler_version = '1.5.12'
        compose_bom_version = '2024.04.01'
        kotlin_version = '1.9.23'
        dagger_version = "2.51.1"
    }
    repositories {
        google()
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.2.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.30.0"
    }
}

plugins {
    id("io.gitlab.arturbosch.detekt").version("1.18.1")
    id 'io.github.gradle-nexus.publish-plugin' version '1.3.0' apply false
    id("com.google.devtools.ksp") version "1.9.23-1.0.20" apply false
}

detekt {
    input = files(rootProject.rootDir)
    config = files("$projectDir/detekt.yml")
    failFast = true
    buildUponDefaultConfig = true
    basePath = projectDir

    reports {
        html.enabled = true
        xml.enabled = true
        sarif.enabled = true
        xml.destination = file("$buildDir/reports/detekt/detekt-checkstyle.xml")
        html.destination = file("$buildDir/reports/detekt/detekt-report.html")
        sarif.destination = file("$buildDir/reports/detekt/detekt-report.sarif")
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

allprojects {
    group = 'com.telefonica.tweaks'
    if (System.getProperty("SNAPSHOT_VERSION") != null) {
        version = System.getProperty("SNAPSHOT_VERSION")+"-SNAPSHOT"
    } else {
        version = System.getProperty("LIBRARY_VERSION") ?: "undefined"
    }
}

apply from: "${rootProject.projectDir}/publish_maven_central.gradle"
